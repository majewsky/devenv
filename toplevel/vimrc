call pathogen#infect()
filetype indent on
filetype plugin on

function! IdentifySyntax()
    return synIDattr(synID(line("."),col("."),1), "name")
endfunction
function! IdentifyHighlight()
    return synIDattr(synIDtrans(synID(line("."),col("."),1)), "name")
endfunction
function! Identify()
    let l_syn = IdentifySyntax()
    let l_hi  = IdentifyHighlight()
    if l_hi != "" && l_hi != "Normal" && l_hi != l_syn
        return l_syn . " -> " . l_hi
    else
        return l_syn
    endif
endfunction

com! IdentifySyntax echo IdentifySyntax()
com! IdentifyHighlight echo IdentifyHighlight()
com! Identify echo Identify()

function! FixCommentsListForCLang()
	" add :/// to comments, but make sure that it has a higher priority than
	set comments-=:// comments+=:///,://
endfunction
com! FixCommentsListForCLang call FixCommentsListForCLang()

function! LowColor()
	set t_Co=8
	colo torte
endfunction
com! LowColor call LowColor()

function! SwitchSourceHeader()
	"update!
	if (expand ("%:e") == "h")
		if (filereadable(expand("%:r") . ".c"))
			find %:r.c
		else
			find %:r.cpp
		endif
	else
		find %:r.h
	endif
endfunction
nmap ,s :call SwitchSourceHeader()<CR>

"config and keybindings for BufExplorer plugin
let g:bufExplorerSortBy='name'

"prevent V-s from being mapped by surround plugin
" (the plugin would just display a deprecation warning anyway)
vnoremap s s

"filetype plugin on
set autoindent
set background=dark
set cindent
set cinoptions=L0,l1,(0,W4
set fo=tcroq
set hlsearch
set incsearch
set ignorecase smartcase
set laststatus=2
set linebreak
set list listchars=trail:·,nbsp:·,tab:\ \ 
set mouse=a
set number
set shiftwidth=4
set expandtab smarttab
set statusline=%<%f\ %#Search#\ %Y%M%R\ %*\ %{fugitive#statusline()}%=\ %-14.(%l,%c%V%)\ %P
set tabstop=4
set wildmode=longest:full
set wildmenu
syntax enable

" remove highlighting of search matches
nmap , :let @/=""<CR>:<BS>
" after repeating commands, put the cursor at the start of the change
nmap . .`[
" after adjusting indentation in visual mode, restore selection for further changes
vnoremap > >gv
vnoremap < <gv
" disable q: command for opening the command line window
map q: <Nop>
" change mouse mode with function keys
nmap <F9> :set mouse=<CR>
nmap <F10> :set mouse=a<CR>

" setup color scheme
set t_Co=256
if $TERM == "xterm-256color"
	let g:ConqueTerm_TERM = 'xterm-256color'
endif

let g:gardener_light_comments=1
colo gardener-transparent

"formatting style for Python code and changelogs in our working group
autocmd FileType python setlocal expandtab smarttab nocindent tabstop=4 softtabstop=4
autocmd FileType python map <F5> :w<CR>:!python "%"<CR>
autocmd FileType changelog setlocal expandtab smarttab tabstop=4 softtabstop=4

autocmd FileType tex map <F5> :w<CR>:wa<CR>:make<CR>
autocmd FileType tex map <F6> :!xdg-open %<.pdf &>/dev/null &disown<CR><CR>
autocmd FileType tex setlocal nocindent

" enable pair matching for angle brackets (e.g. C++ templates)
autocmd FileType cpp setlocal matchpairs+=<:> commentstring=//\ %s

autocmd BufReadPost * DetectIndent

autocmd Syntax      * hi clear SpecialKey | hi link SpecialKey LineNr
autocmd Colorscheme * hi clear SpecialKey | hi link SpecialKey LineNr

hi link perlStatement PreCondit
autocmd Syntax perl call PerlCustomSyntax()
function! PerlCustomSyntax()
    syn keyword perlStatementFlow carp cluck croak confess
    syn keyword perlOperatorNew new
    syn keyword perlConstructor new contained containedin=perlSubName
    hi link perlOperatorNew perlOperator
    hi link perlConstructor perlOperator
endfunction
