#compdef rtree
typeset -A opt_args

(( $+functions[_rtree_zsh_comp] )) || _rtree_zsh_comp()
{
    if (( CURRENT == 2 )); then
        local -a _commands
        _commands=(
            'get:Get the local path for a remote URL (and clone if missing)'
            'index:Operate on the repository index'
        )
        _describe -t commands 'rtree command' _commands
    elif (( CURRENT == 3 )); then
        case "$words[2]" in
            get)
                _alternative "remotes:remote URLs:($(rtree index show | cut -d"$(echo -en '\t')" -f1))"
                ;;
            index)
                local -a _commands
                _commands=(
                    'build:Rebuild the repository index'
                    'show:Print all index entries (as pairs of remote URL and local path)'
                )
                _describe -t commands 'rtree-index command' _commands
                ;;
        esac
    fi
}

_rtree_zsh_comp "$@"
